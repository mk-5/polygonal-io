apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'io.freefair.lombok'

ext {
    artifactId = 'polygonal-architecture'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

jar {
    from sourceSets.main.output
    from project.findProject(':plugin-core').sourceSets.main.output
}

sourcesJar {
    from delombok
    from project.findProject(':plugin-core').delombok
}

gradlePlugin {
    description = "A plugin that helps you to provide polygonal architecture inside your app!"
    website = 'https://polygonal.io'
    vcsUrl = 'https://github.com/mk-5/polygonal-architecture'

    plugins {
        polygonalPlugin {
            id = 'io.polygonal'
            artifactId = 'polygonal-architecture'
            displayName = 'Polygonal Architecture plugin'
            description = 'A plugin that helps you to provide polygonal architecture inside your app!'
            implementationClass = 'io.polygonal.plugin.PolygonalArchitecturePlugin'
            tags = ['architecture', 'polygonal architecture', 'clean architecture']
        }
    }
}
